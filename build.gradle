buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply from: 'build-common.gradle'

sourceCompatibility = targetCompatibility = 1.8

group = package_base
archivesBaseName = mod_id.toUpperCase() + '-' + mc_version
version = (prerelease!="") ? mod_version + '-' + prerelease : mod_version

repositories {
    maven { url 'https://dl.bintray.com/shinoow/maven/' } //AbyssalCraft
    maven { url 'http://files.minecraftforge.net/maven' } //BiomesOPlenty
}

dependencies {
    provided 'com.shinoow.abyssalcraft:AbyssalCraft:'+ac_ver+':api'
    provided 'com.github.glitchfiend.biomesoplenty:BiomesOPlenty:'+bop_ver+':deobf'
}

minecraft {
    setVersion mcf_version
    setMappings mcp_mappings
    setMakeObfSourceJar false
    replace '@MOD_VERSION@', project.version
    replace '0.0-MCF+MINVER', mcf_minver
    replace '9001.0-MCF+MAXVER', mcf_maxver
    if (depstring!='') replace ';after:MODDEPS', ';' + depstring
    else replace ';after:MODDEPS', ''
    replaceIn 'RTG.java'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
		expand ([
			'modid':mod_id,
			'version':mod_version,
			'mcversion':mc_version
		])
	}
	from(sourceSets.main.resources.srcDirs) {exclude 'mcmod.info'}
}
