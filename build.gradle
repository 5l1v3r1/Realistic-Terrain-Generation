buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.ref = parseprops(file('build.properties'))

group = ref.package_base + '.' + ref.mod_id
archivesBaseName = ref.mod_id
version = (System.getenv("TRAVIS") ? "1.9-DEV-" + System.getenv('TRAVIS_BUILD_NUMBER') : ref.mc_version + '-' + ref.mod_version)
versionName = (System.getenv("TRAVIS") ? "1.9-" + ref.mod_version + "-DEV-" +  System.getenv('TRAVIS_BUILD_NUMBER') : version)

minecraft {
    version = ref.mcf_version + '-1.9' //the '-1.9' will probably be dropped in the future once Forge 1.9 is stable, or atleast this seems to be the trend.
    runDir = ref.run_dir
    mappings = ref.mappings
    replace '@MOD_NAME@', ref.mod_name
    replace '@MOD_ID@', ref.mod_id
    replace '@MOD_VERSION@', versionName
    replace '@MCF_VERSION@', ref.mcf_version
    replaceIn 'ModInfo.java'
}

dependencies {}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
		expand ([
			'modid':ref.mod_id,
			'name':ref.mod_name,
			'description':ref.mod_desc,
			'version':ref.mod_version,
			'mcversion':ref.mc_version,
			'url':ref.mod_url,
			'authorList':ref.mod_author,
			'credits':ref.mod_creds,
			'logoFile':ref.mod_logo,
			'dependencies':ref.mod_deps
		])
	}
	from(sourceSets.main.resources.srcDirs) {exclude 'mcmod.info'}
}

def parseprops(File cfg) {
    cfg.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}
